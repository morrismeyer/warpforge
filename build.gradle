import org.gradle.api.tasks.testing.Test

subprojects {

    // Common test logging - show stdout/stderr for all tests including passed
    tasks.withType(Test).configureEach {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            showStandardStreams = true
            showExceptions = true
            showCauses = true
            showStackTraces = true
        }
    }

    // Only create these tasks in Java projects
    plugins.withId('java') {

        tasks.register('cpuTest', Test) {
            description = 'Run CPU-only tests (tagged @Tag("cpu")).'
            group = 'verification'

            useJUnitPlatform {
                includeTags 'cpu'
            }

            // standard test sources / classpath
            testClassesDirs = sourceSets.test.output.classesDirs
            classpath = sourceSets.test.runtimeClasspath
        }

        tasks.register('nvidiaTest', Test) {
            description = 'Run NVIDIA GPU tests (tagged @Tag("nvidia")), excluding nightly.'
            group = 'verification'

            useJUnitPlatform {
                includeTags 'nvidia'
                excludeTags 'nightly'  // BERT EndToEnd tests run nightly only
            }

            testClassesDirs = sourceSets.test.output.classesDirs
            classpath = sourceSets.test.runtimeClasspath
        }

        tasks.register('amdTest', Test) {
            description = 'Run AMD GPU tests (tagged @Tag("amd")), excluding nightly.'
            group = 'verification'

            useJUnitPlatform {
                includeTags 'amd'
                excludeTags 'nightly'  // BERT EndToEnd tests run nightly only
            }

            testClassesDirs = sourceSets.test.output.classesDirs
            classpath = sourceSets.test.runtimeClasspath
        }
    }
}

apply from: "$rootDir/gradle/jdk-routing.gradle"
apply from: "$rootDir/gradle/versions.gradle"

