plugins {
  id 'application'
  id 'org.graalvm.buildtools.native' version '0.10.4'
}

repositories {
  mavenCentral()
}

dependencies {
  implementation project(':snakegrinder-core')
}

application {
  mainClass = 'io.surfworks.snakegrinder.cli.SnakeGrinderCli'
  applicationDefaultJvmArgs = [
    '--enable-native-access=ALL-UNNAMED',
    '-XX:+EnableJVMCI'
  ]
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(25)
  }
}

def j25Launcher = javaToolchains.launcherFor {
  languageVersion = JavaLanguageVersion.of(25)
}

tasks.withType(JavaExec).configureEach {
  javaLauncher.set(j25Launcher)
}

tasks.withType(Test).configureEach {
  javaLauncher.set(j25Launcher)
}

// Native image configuration for GraalPy embedding
graalvmNative {
  binaries {
    main {
      imageName = 'snakegrinder'
      mainClass = 'io.surfworks.snakegrinder.cli.SnakeGrinderCli'

      // Native image build options
      buildArgs.addAll([
        '-H:+ReportExceptionStackTraces',
        '--no-fallback',
        // Include Python resources from snakegrinder-core
        '-H:IncludeResources=snakegrinder/.*\\.py$',
        // GraalPy optimizations - reduce binary size
        '-Dpython.WithoutSSL=true',
        '-Dpython.WithoutCompressionLibraries=true',
        '-Dpython.WithoutNativePosix=true'
      ])
    }
  }

  toolchainDetection = false
}
