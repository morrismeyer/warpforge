plugins { id 'java-library' }

group = 'io.surfworks.warpforge'
version = '0.0.1-SNAPSHOT'

repositories { mavenCentral() }

dependencies {
    // Core tensor types - but avoid snakeburger-core dependency (requires Babylon JDK)
    // warpforge-data should work with standard JDK 25

    // JSON parsing for SafeTensors headers and HuggingFace API
    implementation 'com.google.code.gson:gson:2.11.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['--enable-preview']
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    jvmArgs '--enable-preview'
}

test {
    useJUnitPlatform {
        excludeTags 'integration', 'slow'
    }
}

tasks.register('integrationTest', Test) {
    description = 'Run integration tests (downloads, network access)'
    group = 'verification'

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    useJUnitPlatform {
        includeTags 'integration'
    }

    testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut'
        showStandardStreams = true
    }
}
